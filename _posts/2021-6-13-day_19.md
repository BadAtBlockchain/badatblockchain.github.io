---
layout: post
title: "Day 19"
author: "Bad"
tags: 100daysofcode
---

### Digging Into the Open Zeppelin Wizard Results

Today I wanted to test my skills at reading and documenting some Solidity that I haven't written or had any hands on time to edit or tweak. I am going to generate a small ERC20 contract using the wizard I included on day 15. As this wizard follows industry best practices and generates everything based on the agreed standards of the tokens, it's probably the best form of code I can look at, in a raw format.

To begin with, here are the settings I chose within the wizard. I can correlate the following code snippets and descriptions with where these are included for completion:

![Wizard settings](/assets/images/day_19/1_wizard_settings.png)

Breaking this down into an easy list to reference (via numbers), here we are:

1. Name
2. Symbol (Ticker)
3. Premint amount
4. Token is Burnable
5. Token is Pausable
6. Token is Ownable

The next snippet is a little unbelievable. The included code isn't exactly long, which is what I find hard to believe. But this is what an ERC20 compliant token with the 6 features above actually looks like...

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol";
import "@openzeppelin/contracts/security/Pausable.sol";
import "@openzeppelin/contracts/access/Ownable.sol";

contract StillANewb is ERC20, ERC20Burnable, Pausable, Ownable {
    constructor() ERC20("StillANewb", "SAN") {
        _mint(msg.sender, 1000 * 10 ** decimals());
    }

    function pause() public onlyOwner {
        _pause();
    }

    function unpause() public onlyOwner {
        _unpause();
    }

    function _beforeTokenTransfer(address from, address to, uint256 amount)
        internal
        whenNotPaused
        override
    {
        super._beforeTokenTransfer(from, to, amount);
    }
}
```

Crazy right?! Now imaging what it would look like it the token was not pausable or burnable... it would be even more basic! I know right, don't believe me?! Well here you are:

```javascript
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract StillANewb is ERC20 {
    constructor() ERC20("StillANewb", "SAN") {
        _mint(msg.sender, 1000 * 10 ** decimals());
    }
}
```

But ignore that one, I'm going to focus on the first snippet, so let's dig in!

### Contract Inheritance

16 days ago (wow, that has both flown by and taken it's time) I first touched on inheritance within the Solidity. With that, I wrote a small contract that used a custom interface and a custom abstract contract to implement features across a hierarchy of contracts. The Open Zeppelin contracts take advantage of this. In my opinion, and probably the opinion of others, standardising tokens in this way ultimately helps with modularity and fast development. Being able to spin up a new token contract (.sol file) and simply import and inherit from a range of contracts very quickly gives you a fully functioning contract (as proven by the wizard). Naturally, this also means updating local codebases can be done from a single point, globally updating every contract that inherits from it (ignoring the deployed contracts of course).

As can be seen, the contract declaration inherits from 4 contracts. These are:

- [ERC20](https://docs.openzeppelin.com/contracts/4.x/api/token/erc20#erc20) ("@openzeppelin/contracts/token/ERC20/ERC20.sol")
- [ERC20Burnable](https://docs.openzeppelin.com/contracts/4.x/api/token/erc20#ERC20Burnable) ("@openzeppelin/contracts/token/ERC20/extensions/ERC20Burnable.sol")
- [Pausable](https://docs.openzeppelin.com/contracts/4.x/api/security#Pausable) ("@openzeppelin/contracts/security/Pausable.sol")
- [Ownable](https://docs.openzeppelin.com/contracts/4.x/api/access#Ownable) ("@openzeppelin/contracts/access/Ownable.sol")

I don't want to dig too deep into these inherited classes as they have their own inheritance and in the end I'll literally just rewriting the Open Zeppelin documentation... and that already exists. What I will do though, is include links to the contract source on Github and note down the common functionality that a user would likely want to know about (from my point of view and current understanding).

First off, the ERC20 contract. Source: [https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/ERC20.sol)

This is the backbone of the contract. As discussed,  the wizard was used to generate an ERC20 compliant token and it pretty much just gave us a constructor. The ERC20 contract gives the token the majority of functionality that I will need and helps keep in-line with token standards. Some functions that we care about immediate include:

- totalSupply
- balanceOf
- transfer
- transferFrom
- _mint
- _burn

From my limited experience, the above list of functions pretty much handles everything a token/cryptocurrency needs to function at a basic level. The names of the functions are self explanatory so I will let the reader dig in deeper with documentation and source code if need be.

ERC20 Burnable. Source: [https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/extensions/ERC20Burnable.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC20/extensions/ERC20Burnable.sol)

Relatively self explanatory from the contract name, but a user can burn tokens from the total supply and balance if they choose to. Burning is an important part of a tokens tokenomics and can help overcome supply/demand issues, as well as build trust and follow roadmaps. The contract includes 2 separate functions that are both publicly exposed and can be used freely by anyone:

- burn
- burnFrom

If the implemented require statements (rules) pass, these functions eventually just end up calling an ERC20 contract function (due to further inheritance), _burn. This is discussed further down.

Pausable. Source: [https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/Pausable.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/security/Pausable.sol)

Overall, a pretty simple contract. It holds a state variable, specifically a boolean value that controls the paused state of the contract itself. As this variable is private, a getter function is implemented as well as 2 separate internal setters (one to pause, one to unpause). Finally, 2 modifiers are implemented to help the contract's execution flow. These are touched on a little later on.

Ownable: Source: [https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/access/Ownable.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/access/Ownable.sol)

Similar to the pausable contract above, the ownable contract contains a state variable, specifically an address value. This references the current owner of the contract. Some functionality to transfer and renounce ownership (providing the requires pass). A getter to disclose who the current owner of the contract is and finally a modifier that is used throughout the contract to help with execution flow.

### Constructing the Token

So I included the ERC20 token, and the initial contract constructor seems pretty empty. Initially, the reader may think "Well, how does the ERC20 constructor now it should be called. When does that actually fire?"

```javascript
constructor() ERC20("StillANewb", "SAN") {
	_mint(msg.sender, 1000 * 10 ** decimals());
}
```

Looking at the above, I can see on the first line that the ERC20 constructor is actually called there with the token 'name' (1) and 'symbol' (2) passed in. This constructor doesn't do anything too special, other than set 2 state variables for the underlying token:

```javascript
// vars defined in the contract:
string private _name;
string private _symbol;

// snipped...
constructor(string memory name_, string memory symbol_) {
    _name = name_;
	_symbol = symbol_;
}
```

Glorious! I now know how to go from the constructor, through to the inherited ERC20 constructor. Easy right?

I can also see a function that is called in the initial constructor. Once the ERC20 constructor has been called, the_mint() function is called. From the overall contract, I can see that it doesn't actually have a _mint() function implemented. From naming convention, I know this must be a private function (due to the underscore at the beginning). So it must be called from somewhere within an inherited contract. The most logical place to look would be the included ERC20 contract:

```javascript
function _mint(address account, uint256 amount) internal virtual {
    require(account != address(0), "ERC20: mint to the zero address");

    _beforeTokenTransfer(address(0), account, amount);

    _totalSupply += amount;
    _balances[account] += amount;
	emit Transfer(address(0), account, amount);
}
```

Let's break this down a little bit:

1. First of all, the function is passed an address and an amount. In this case, the address is the address of the contract deployer/owner (msg.sender) and the amount if the pre-mint amount (3) defined in the wizard (together with some math to help with token decimals).
2. A require statement ensures that the address is not a zero/null address.
3. A hook (not discussed yet) is then called.
4. The initial supply and deplorer's wallet is updated to hold the entire balance of the token.
5. An event is emitted for use with front end/debuggers/pipeline tools/tests (whatever).

### The Ability to Pause... But Pause What?

Some included functionality within the generated wizard contract appears to be the publicly visible functions to pause the contract. But what does this actually do?

```javascript
function pause() public onlyOwner {
	_pause();
}

function unpause() public onlyOwner {
	_unpause();
}
```

As the name states... it pauses the contract. Remember early when I mentioned a hook that is called during the _mint() process? Well, this is where it becomes important (as far as I can tell, please correct me if I'm wrong). The _beforeTokenTransfer() hook is called, ultimately ensuring some specific logic occurs before anything happens to the token and it's supply. At this time, I haven't seen this function just yet, but when using the wizard, we were provided one:

```javascript
function _beforeTokenTransfer(address from, address to, uint256 amount)
	internal
	whenNotPaused
	override
{
	super._beforeTokenTransfer(from, to, amount);
}
```

What's interesting about this, is it doesn't actually look like it does anything... it just simply calls the hook that is implemented within the super (base class). Likely the ERC20 contract:

```javascript
function _beforeTokenTransfer(
    address from,
    address to,
    uint256 amount
) internal virtual {}
```

Helpful right? No. What is important here though, is the modifier used within the contract's implementation of the target function. As this is an override - it will be hooked before the super contracts implementation! "whenNotPaused" is one of the modifiers that was has been implemented in the Pausable contract:

```javascript
modifier whenNotPaused() {
	require(!paused(), "Pausable: paused");
    _;
}
```

This is a pretty simple modifier. It just ensures that the current state of the contract is not paused. If it isn't and the require() statement passes, then the execution flow continues. I guess this pretty much answers the question of how the pausing actually works, right? This hook appears to be called by everything that does *something* with the token supply. A second example of this hook can be seen the _transfer function of ERC20:

```javascript
function _transfer(
    address sender,
    address recipient,
    uint256 amount
) internal virtual {
    require(sender != address(0), "ERC20: transfer from the zero address");
    require(recipient != address(0), "ERC20: transfer to the zero address");

    _beforeTokenTransfer(sender, recipient, amount);

    uint256 senderBalance = _balances[sender];
    // ... snipped
```

The main reason behind including this contract within the main deployment is the power to pause everything incase of a security issue. For example, if a bug exists to allow a user to just mint new tokens, or transfer tokens from others without them knowing (etc), the owner can stop everything to assess the situation. If the owner has been renounced (loosely mentioned within the Ownable contract) then it cannot be paused. So renouncing the ownership should only be done when everything is tried, tested and confirmed to be 'safe'. This is the same with transferring ownership, however it must factor in the new owner and how much they can be trusted and their intent.

### Let It Burn!!!

Burning has been mentioned briefly thus far, but I wanted to dig in a little deeper to upgrade my knowledge from 'helps the tokenomics' to an actual understanding of what it does and how it does it. Looking at the _burn function within the ERC20 contract, a couple of things happen:

```javascript
function _burn(address account, uint256 amount) internal virtual {
    require(account != address(0), "ERC20: burn from the zero address");

    _beforeTokenTransfer(account, address(0), amount);

    uint256 accountBalance = _balances[account];
    require(accountBalance >= amount, "ERC20: burn amount exceeds balance");
    unchecked {
        _balances[account] = accountBalance - amount;
    }
    _totalSupply -= amount;

    emit Transfer(account, address(0), amount);
}
```

1. Ensures a burn is relative to a real address
2. Calls the _beforeTokenTransfer hook (think back the the 'paused' state implementation)
3. Gets the current balance for the address and ensures the target burn amount is less than the total
4. Using unchecked** math, take the target amount away from the balance and update the state variable balance
5. Remove the amount from the total supply
6. Emit an event acknowledging this change

**The unchecked keyword is one I haven't come across yet. Since Solidity 0.8.0, all math is protected from overflows without the developer needing to include the SafeMath library. The unchecked keyword simply forces Solidity to ignore these checks and use the original math. In this case, the require statement ensures the math won't overflow anyway, making sure the amount to subtract is already less than (or equal to) the total so the value will be 0 or higher, regardless. I believe this can be done to help optimise gas costs with the transactions and providing the logic and rules are met for everything, then if the developer can save some gas in general and controls the flow with careful steps, then it doesn't seem like a bad choice. More information on unchecked can be read [here](https://docs.soliditylang.org/en/v0.8.0/control-structures.html#checked-or-unchecked-arithmetic).

The main area of this function that should be properly understood is the included math. After the checks are done, and all require statements are passed. The tokens are literally just 'removed' from the target users balance and overall supply. I know that seems pretty blunt and not very well explained. But due to the way tokens are minted and how blockchain is stored/immutable, if a function within the smart contract states that the total supply changes or a user's balance changes, then that's what the value is going forward. This is simply done by changing the global value, ultimately 'deleting' the possibility of ever owning that portion of the token again. When put into perspective with relation to real money... this sounds a little scary, and makes one think "Why would we pay money for something that is just burnt and deleted forever". I guess real life has a lot of these things too, but atleast they primarily physical... right? Anyway, I digress. Burning is cool. It's simple, but effective and can have a huge impact (positive and negative) on the tokenomics of a project.

### Quick Recap

I dug in to the generated ERC20 contract, highlighted key functionality and explained how the different settings are implemented and what they bring to the contract. Overall, the wizard is awesome and can pretty much help anyone get a token up and running with very little effort. We can all take peace in knowing that standards are met, core concepts and implemented and the code is audited and recommended for use. If anything, it's an educational lesson and a lot can be learnt using a wizard/code generator. Just be sure to understand it before you ape in (or not, you do you!).